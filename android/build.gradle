// Top-level build file where you can add configuration options common to all sub-projects/modules.

import groovy.json.JsonSlurper

def getNpmVersion() {
    try {
        def inputFile = new File("../package.json")
        def packageJson = new JsonSlurper().parseText(inputFile.text)
        return packageJson["version"]
    } catch(Exception ex) {
        throw new RuntimeException("Unable to read ../package.json file. " +
                "This is required to set the version number for the Android build.", ex)
    }
}

def getNpmVersionArray() { // major [0], minor [1], patch [2]
    def npmVersion = getNpmVersion()
    if (npmVersion == null) {
        logger.warn('No version found in PROJECT_ROOT/package.json: defaulting with version 1.0.0')
        return [1, 0, 0]
    } else {
        try {
            def (major, minor, patch) = npmVersion.tokenize('.')
            return [Integer.parseInt(major), Integer.parseInt(minor), Integer.parseInt(patch)] as int[]
        } catch(Exception ex) {
            throw new RuntimeException("Unable to parse version info found in ../package.json " +
                    "into [major].[minor].[patch] format: " + npmVersion, ex)
        }
    }
}

subprojects {
    ext {
        def npmVersion = getNpmVersionArray()
        versionMajor = npmVersion[0]
        versionMinor = npmVersion[1]
        versionPatch = npmVersion[2]
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
    }
}
